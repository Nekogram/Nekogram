plugins {
    id 'com.android.application' version '8.4.0' apply false
    id 'com.google.gms.google-services' version '4.3.10' apply false
    id 'com.google.firebase.crashlytics' version '2.9.9' apply false
    id 'org.lsposed.lsparanoid' version '0.5.2'
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("com.android.library") ||
                plugins.hasPlugin("com.android.application")) {
            android {
                compileSdkVersion 34
                buildToolsVersion "34.0.0"
                ndkVersion "23.2.8568313"

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 33
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }

                buildFeatures.buildConfig = true
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
            }
        }
    }
}
