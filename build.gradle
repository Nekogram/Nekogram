// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '6.5.0.202303070854-r'
    }
}

plugins {
    id 'com.android.application' version '8.0.0' apply false
    id 'com.google.gms.google-services' version '4.3.10' apply false
    id 'org.lsposed.lsparanoid' version '0.5.2'
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("com.android.library") ||
                plugins.hasPlugin("com.android.application")) {
            android {
                compileSdkVersion 33
                buildToolsVersion "33.0.2"
                ndkVersion "23.2.8568313"

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 33
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }

                buildFeatures.buildConfig = true
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}